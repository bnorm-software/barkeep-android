buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }

    // Exclude the lombok version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco-android'
apply plugin: 'me.tatarka.retrolambda'

def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId "com.bnorm.barkeep"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 4
        versionName "1.3"

        testInstrumentationRunner "com.bnorm.barkeep.BarkeepTestRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    dataBinding {
        enabled = true
        version = "2.0.0"
    }
}

dependencies {
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    compile project(path: ':backend', configuration: 'android-endpoints')
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.github.tony19:logback-android-core:1.1.1-5'
    compile 'com.github.tony19:logback-android-classic:1.1.1-5'

    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.trello:navi:0.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'

    apt 'com.google.dagger:dagger-compiler:2.4'
    compile 'com.google.dagger:dagger:2.4'

    // conflicting production and test dependencies
    compile 'com.google.code.findbugs:jsr305:2.0.1'  // used by backend
    androidTestCompile 'com.android.support:support-annotations:23.3.0'


    // test dependencies
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.27'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoAndroidUnitTestReport {
    excludes += [
            // android related
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*',
            // data binding
            '**/*Binding.class',
            // retro lambda
            '**/Lambda$*.class', '**/Lambda.class', '**/*Lambda.class', '**/*Lambda*.class',
            // butter knife
            '**/*$ViewBinder*.*', '**/*$ViewInjector*.*',
            // auto-value
            '**/AutoValue_*.*',
            // dagger
            '**/*_Factory.class', '**/*_MembersInjecter.class',
    ]
}