apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId "com.bnorm.barkeep"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 4
        versionName "1.3"

        testInstrumentationRunner "com.bnorm.barkeep.BarkeepTestRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'

    compile 'com.jakewharton.timber:timber:4.3.1'
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.trello.navi2:navi:2.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'

    apt 'com.google.dagger:dagger-compiler:2.10'
    compile 'com.google.dagger:dagger:2.10'

    // conflicting production and test dependencies
    androidTestCompile 'com.android.support:support-annotations:25.2.0'


    // test dependencies
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.30'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoAndroidUnitTestReport {
    excludes += [
            // android related
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*',
            // data binding
            '**/*Binding.class',
            // retro lambda
            '**/Lambda$*.class', '**/Lambda.class', '**/*Lambda.class', '**/*Lambda*.class',
            // butter knife
            '**/*$ViewBinder*.*', '**/*$ViewInjector*.*',
            // auto-value
            '**/AutoValue_*.*',
            // dagger
            '**/*_Factory.class', '**/*_MembersInjecter.class',
    ]
}